(*
Walking
=======

The Walking file includes functions & procedures that have anything to do with
walking. You'll notice the defines ({$IFDEF REFLECTION}) used throughout these
methods; these are used so MSI will still compile if the user decides to exclude
reflection or if reflection is broken. This file handles walking for both SPS
and reflection.

The source for Walking.simba can be found
`here <https://github.com/SRL/MSI/raw/master/MSI/Core/Walking.simba>`_.

*)

(*
MSI_Flag
~~~~~~~~

.. code-block:: pascal

  procedure MSI_Flag(FlagDist: Integer);

MSI's custom flag function. It waits until the flag is within 'FlagDist' from
the player. It also works if, for some reason, the flag has gone off the minimap,
but the player is still moving.

.. note::

  | Author: The MSI Team
  | Last Updated: 26 January 2011 by Coh3n

Example:

.. code-block:: pascal

  if (MSI_WalkToPoint(MSI_GetWalkCoords(0))) then
    MSI_Flag(MIN_FLAG_DISTANCE); // MIN_FLAG_DISTANCE is found in Globals.simba
*)
procedure MSI_Flag(FlagDist: Integer);
var
  t: Integer;
begin
  if (not LoggedIn) then
    Exit;

  repeat
    if (LeaveDangerZone) then
      MSI_Debug('Left danger zone');

    Wait(RandomRange(200, 500));
    t := (GetSystemTime + 12000);

    // If the player is going around an obstacle and the flag goes off the minimap
    if ((not FlagPresent) and (IsMoving)) then
    begin
      MSI_Antiban(RandomRange(300, 1000), ANTI_BAN_CHANCE);
      Continue;
    end;

    if (FlagDistance <= FlagDist) or (not IsMoving) then
      Exit;

    MSI_FindRandoms(False);
    MSI_AntiBan(RandomRange(200, 800), ANTI_BAN_CHANCE);

    MSI_SubDebug('Flag distance: '+IntToStr(FlagDistance));

  until((not (FlagPresent)) and (not (IsMoving))) or (t < GetSystemTime);
end;

(*
MSI_WaitTile
~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_WaitTile(Tile: TPoint): Boolean;

Simple, yet effective function used as a substitute for Flag functions.
MSI_WaitTile returns true with 'Tile' is on the minimap. See MSI_WalkTile to
see exactly how it's used.

.. note::

  | Author: Coh3n
  | Last Updated: 26 January 2011 by Coh3n

Example:

.. code-block:: pascal

  if (MSI_ClickTile(i)) then
    MSI_WaitTile(i + 1); // Waits for the next tile in the path
*)
function MSI_WaitTile(Tile: TPoint): Boolean;
{$IFDEF REFLECTION}
var
  t: Integer;
{$ENDIF}
begin
  if (not LoggedIn) then
    Exit;

  {$IFDEF REFLECTION}
  t := GetSystemTime + 15000; // Maximum wait time is 15 seconds
  repeat
    if (rs_GetCompassAngleDegrees <> 0.0) then
      clickNorth(true);

    if (LeaveDangerZone) then
      MSI_Debug('Left danger zone');

    Result := TileOnMM(Tile);

    MSI_WaitWhileResting;
    MSI_FindRandoms(False);
    MSI_AntiBan(RandomRange(200, 800), ANTI_BAN_CHANCE);
    Wait(RandomRange(200, 500));
  until(Result or (GetSystemTime > t) or (not LoggedIn));
  {$ENDIF}
end;

(*
MSI_ClickTile
~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_ClickTile(Tile: TPoint): Boolean;

Checks to see if the player is near 'Tile', if so, returns true, otherwise it
will verify the tile is on the minimap and return true when clicked.

.. note::

  | Author: marpis
  | Last Updated: 27 March 2011 by Coh3n

Example:

.. code-block:: pascal

  if (MSI_ClickTile(Point(1234, 4321))) then
    MSI_Debug('Clicked tile');
*)
function MSI_ClickTile(Tile: TPoint): Boolean;
{$IFDEF REFLECTION}
var
  MP: TPoint;
{$ENDIF}
begin
  if (not LoggedIn) then
    Exit;

  {$IFDEF REFLECTION}
  MP := TileToMM(Tile);

  if (Distance(MP.X, MP.Y, MMCX, MMCY) <= 5) then
  begin
    MSI_Debug('Already near tile');
    Result := True;
    Exit;
  end;

  if (TileOnMM(Tile)) then
  begin
    MSI_Debug('Tile('+IntToStr(Tile.x)+', '+IntToStr(Tile.y)+') is on the minimap');
    MSI_MultiClickMouse(MP.X, MP.Y, 60, 4);
    Result := True;
    RunEnergy(20);
  end else
    MSI_Debug('Tile('+IntToStr(Tile.x)+', '+IntToStr(Tile.y)+') is not on the minimap');
  {$ENDIF}
end;

(*
MSI_PointOnMM
~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_PointOnMM(p: TPoint; var Coord: TPoint): Boolean;

Returns try if the point, p, is on the minimap. P can be a tile or an SPS point.
The coordinates on the minimap are set to 'Coord'.

.. note::

  | Author: Coh3n
  | Last Updated: 27 February 2011 by Coh3n

Example:

.. code-block:: pascal

  if (MSI_PointOnMM(Path[i], p)) then
    Mouse(p.x, p.y, 3, 3, True);
*)
function MSI_PointOnMM(p: TPoint; var Coord: TPoint): Boolean;
begin
  if (not LoggedIn) then
    Exit;

  if (ReflectionBroken) then
    {$IFDEF SPS}
    Coord := SPS_PosToMM(p)
    {$ELSE}
    Wait(50)
    {$ENDIF}
  else
    {$IFDEF REFLECTION}
    Coord := TileToMM(p);
    {$ELSE}
    Wait(50);
    {$ENDIF}

  Result := rs_OnMinimap(Coord.x, Coord.y);
  MSI_SubDebug('MSI_PointOnMM'+ToStr(p)+': ' + BoolToStr(Result));
end;

(*
MSI_Relocate
~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_Relocate(Loc: Integer): Boolean;

Relocates the player to the location 'Loc'.  Only relocates if the player is
near the location.  Returns true if relocation was successful.

.. note::

  | Author: Sir R. Magician
  | Last Updated: 22 June 2011 by Coh3n

Example:

.. code-block:: pascal

  if (MSI_Relocate(LOC_VE_MINE)) then
    MSI_Debug('Successfully relocated');
*)
function MSI_Relocate(Loc: Integer): Boolean;
var
  i, t, cp: Integer;
  p: TPoint;
  TPA: TPointArray;
  atTile: boolean;
begin
  if (not LoggedIn) then
    Exit;

  MSI_AddHeader('MSI_Relocate');

  if (ReflectionBroken) then
  begin
    TPA := MSI_Locations[Loc].Points
  end else
    TPA := MSI_Locations[Loc].Tiles;

  if (Length(TPA) = 0) then
  begin
    MSI_SubDebug('No relocation points for ' + MSI_Locations[Loc].Name);
    Exit;
  end;

  cp := MSI_Players[CurrentPlayer].CurrentPoint;
  if (cp = High(TPA)) then
  begin
    InvertTPA(TPA);
    cp := High(TPA) - cp;
  end;
  MSI_Players[CurrentPlayer].CurrentPoint := cp;

  t := (GetSystemTime + 10000);
  while (not Result) and (LoggedIn) and (GetSystemTime < t) do
  begin
    for i := cp to High(TPA) do
    begin
      if (not MSI_PointOnMM(TPA[i], p)) then
      begin
        if (MSI_FindRandoms(True)) then
        begin
          MSI_CloseHeader('MSI_Relocate: ' + boolToStr(result));
          result := true;
          exit;
        end;

        wait(RandomRange(200, 300));

        if ((i = high(TPA)) and (atTile)) then
        begin
          result := true;
          break;
        end else
          if (i = high(TPA)) then
            break;

        Continue;
      end;

      if (Distance(p.x, p.y, MMCX, MMCY) > MSI_Locations[Loc].MinDistance) then
      begin
        Mouse(p.x, p.y, 4, 4, True);
        MSI_Debug('Walking to Point(' + IntToStr(p.x) + ', ' + IntToStr(p.y) + ')');
        MSI_Flag(0);
        Result := True;
        Break;
      end else begin
        atTile := true;
        if (i = High(TPA)) then
        begin
          MSI_Debug('Already at ' + MSI_Locations[Loc].Name + ' Point('+IntToStr(i)+')');
          Result := True;
        end;
      end;
    end;

    if (not Result) then
      cp := 0
    else
      cp := i;
  end;

  if (not Result) then
  begin
    {$IFDEF REFLECTION}
    if (not ReflectionBroken) then
      if (Length(MSI_Locations[Loc].Points) > 0) then
      begin
        MSI_Debug('Trying r_WebWalk');
        Result := r_WebWalk(MSI_Locations[Loc].Points[0]);
      end;
    {$ENDIF}
  end;

  MSI_Players[CurrentPlayer].CurrentPoint := cp;
  MSI_CloseHeader('MSI_Relocate: ' + BoolToStr(Result));
end;

(*
MSI_AtLocation
~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_AtLocation(Loc: Integer; Relocate: Boolean): Boolean;

Returns true if the player is at the location 'Loc'.  Works with both reflection
and SPS. If the player isn't at the location and 'Relocate' is set to true,
will call MSI_Relocate.

.. note::

  | Author: Coh3n
  | Last Updated: 27 April 2011 by Coh3n

Example:

.. code-block:: pascal

  if (MSI_AtLocation(LOC_VE_BANK, True)) then
    MSI_Debug('We are at Varrock East Bank');
*)
function MSI_AtLocation(Loc: Integer; Relocate: Boolean): Boolean;
var
  i, d: Integer;
  p: TPoint;
  TPA: TPointArray;
begin
  if (not LoggedIn) then
    Exit;

  MSI_AddHeader('MSI_AtLocation');

  if (Loc = LOC_POWER_SKILL) then
  begin
    MSI_Debug('We are powerskilling, we have no specific location');
    Result := True;
  end;

  if (not Result) then
  begin
    if (ReflectionBroken) then
    begin
      TPA := MSI_Locations[Loc].Points
    end else
      TPA := MSI_Locations[Loc].Tiles;

    for i := 0 to High(TPA) do
    begin
      MSI_Debug('Checking Point: ' + ToStr(TPA[i]));

      if (MSI_PointOnMM(TPA[i], p)) then
      begin
        d := Distance(p.x, p.y, MMCX, MMCY);
        MSI_Debug('Distance from player to ' + MSI_Locations[Loc].Name + ': ' + IntToStr(d));

        Result := (d <= 30);
        if (not Result) and (Relocate) then
          Result := MSI_Relocate(Loc);

        if (Result) then
        begin
          MSI_Debug('Player is at ' + MSI_Locations[Loc].Name);
          MSI_Players[CurrentPlayer].Location := MSI_Locations[Loc].Constant;
          Break;
        end;
      end;
    end;
  end;

  if (not Result) then
    MSI_Debug('Player not at ' + MSI_Locations[Loc].Name);

  MSI_CloseHeader('MSI_AtLocation: ' + BoolToStr(Result));
end;

(*
MSI_LoadPath
~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_LoadPath(sLoc, eLoc: Integer; Color: Boolean): T2DPointArray;

Loads either a reflection or color path depending on 'Color' boolean. Loads the
path from the starting location (sLoc) to the ending location (eLoc).

.. note::

  | Author: Coh3n
  | Last Updated: 27 February 2011 by Coh3n

Example:

.. code-block:: pascal

  MSI_LoadPath(LOC_VE_BANK, LOC_VE_MINE, True); // Loads color path from VE Bank to VE Mine
*)
function MSI_LoadPath(sLoc, eLoc: Integer; Color: Boolean): T2DPointArray;
var
  Script: TScript;
  s: string;
begin
  if (not LoggedIn) then
    Exit;

  Script := MSI_Scripts[MSI_Players[CurrentPlayer].Scripts[CurrentScript].Name];

  if (sLoc = Script.PathInfo.StartLoc) then
    if (Color) then
      Result := Script.PathInfo.Color
    else
      Result := Script.PathInfo.Reflection
  else
    if (sLoc = Script.PathInfo.EndLoc) then
      if (Color) then
        Result := MSI_MirrorATPA(Script.PathInfo.Color)
      else
        Result := MSI_MirrorATPA(Script.PathInfo.Reflection);

  if (Color) then
    s := 'Color'
  else
    s := 'Reflection';

  MSI_SubDebug('Loaded Path (' + s + '): '+ToStr(Result));
end;

(*
MSI_WalkColorPath
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_WalkColorPath(Points: TPointArray): Boolean;

Uses SPS's path walking to walk the path specified by 'Points'.  Each script's
path is defined with the script's record in Scripts.simba.

.. note::

  | Author: Coh3n
  | Last Updated: 27 February 2011 by Coh3n

Example:

.. code-block:: pascal

  with MSI_Players[CurrentPlayer] do
    MSI_WalkColorPath(Scripts[CurrentScript].PathInfo.Color);
*)
function MSI_WalkColorPath(Points: TPointArray): Boolean;
begin
  if (not LoggedIn) then
    Exit;

  {$IFDEF SPS}
  Result := SPS_WalkPath(Points);
  {$ENDIF}
end;

(*
MSI_WalkTilePath
~~~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_WalkTilePath(tPath: TPointArray): Boolean;

Walks the entire tile path (tPath). Uses MSI_WaitTile for every tile except the
last one (uses MSI_Flag(0) instead) to ensure the player has stopped moving
before searching for objects on the mainscreen.

.. note::

  | Author: marpis & Coh3n
  | Last Updated: 23 June 2011 by Coh3n

Example:

.. code-block:: pascal

  if (MSI_WalkTilePath(TilePathHere)) then
    MSI_Debug('Walked path');
*)
function MSI_WalkTilePath(tPath: TPointArray): Boolean;
{$IFDEF REFLECTION}
var
  h, i, t: Integer;
  mPos: TPoint;
  stopMoving: Boolean;
{$ENDIF}
begin
  if (not LoggedIn) then
    Exit;

  {$IFDEF REFLECTION}
  with MSI_Players[CurrentPlayer] do
  begin
    t := GetSystemTime + (Length(tPath) * 60 + 180) * 1000; // A minute for each tile + 3 minutes
    h := High(tPath);

    repeat
      for i := h downto 0 do
      begin
        stopMoving := (i = h);

        if (not MSI_ClickTile(tPath[i])) then
        begin
          if (i = 0) then
            Exit;

          Continue;
        end else
          if (stopMoving) then
            MSI_Flag(0)
          else
            MSI_WaitTile(tPath[i + 1]);

        mPos := GetMyPos;
        Result := (Distance(mPos.x, mPos.y, tPath[h].x, tPath[h].y) <= 10);
        Break;
      end;
    until(Result or (GetSystemTime > t));
  end;

  if (not result) then
    if (length(tPath) > 0) then
    begin
      MSI_SubDebug('Regular walking failed, attempting blind walk');
      result := r_WebWalk(tPath[high(tPath)]);
    end;
  {$ENDIF}
end;

(*
MSI_DeathWalk
~~~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_DeathWalk: Boolean;

Attempts to walk to the starting location for each respective script if the
player is lost. Teleports home and walks back if need be.

.. note::

  | Author: NCDS
  | Last Updated: 5 July 2011 by NCDS

Example:

.. code-block:: pascal

  if MSI_DeathWalk then
    MSI_Debug('Back on track!');
*)
function MSI_DeathWalk: Boolean;
var
  pArr, Path: T2DPointArray;
  ePath: TPointArray;
  Method: function(tpa: TPointArray): Boolean;
  obs: TIntegerArray;
  Script: TScript;
  e, i: Integer;
  p: TPoint;
begin
  if (not LoggedIn) then
    Exit;
  MSI_AddHeader('MSI_DeathWalk');

  Script := MSI_Scripts[MSI_Players[CurrentPlayer].Scripts[CurrentScript].Name];

  {load paths}
  case Script.PathInfo.StartLoc of
    LOC_VE_BANK: pArr := [TPointArray([Point(3230, 3217), Point(3242, 3224), Point(3257, 3226),
             Point(3260, 3238), Point(3254, 3250), Point(3251, 3263),
             Point(3242, 3275), Point(3239, 3290), Point(3241, 3306),
             Point(3251, 3317), Point(3264, 3324), Point(3278, 3332),
             Point(3287, 3345), Point(3296, 3356), Point(3296, 3369),
             Point(3294, 3382), Point(3292, 3396), Point(3290, 3409),
             Point(3285, 3422), Point(3277, 3429), Point(3263, 3429),
             Point(3255, 3420)])];
    LOC_VW_BANK: pArr := [TPointArray([Point(3230, 3217), Point(3242, 3224), Point(3257, 3226),
             Point(3260, 3238), Point(3254, 3250), Point(3251, 3263),
             Point(3242, 3275), Point(3239, 3290), Point(3241, 3306),
             Point(3251, 3317), Point(3264, 3324), Point(3278, 3332),
             Point(3287, 3345), Point(3296, 3356), Point(3296, 3369),
             Point(3294, 3382), Point(3292, 3396), Point(3290, 3409),
             Point(3285, 3422), Point(3279, 3429), Point(3264, 3428),
             Point(3252, 3429), Point(3238, 3430), Point(3224, 3429),
             Point(3208, 3429), Point(3194, 3430), Point(3185, 3438)])];
    LOC_GE_BANK: pArr := [TPointArray([Point(3230, 3217), Point(3242, 3224), Point(3257, 3226),
             Point(3260, 3238), Point(3254, 3250), Point(3251, 3263),
             Point(3242, 3275), Point(3239, 3290), Point(3241, 3306),
             Point(3251, 3317), Point(3264, 3324), Point(3278, 3332),
             Point(3287, 3345), Point(3296, 3356), Point(3296, 3369),
             Point(3294, 3382), Point(3292, 3396), Point(3290, 3409),
             Point(3285, 3422), Point(3279, 3429), Point(3264, 3428),
             Point(3252, 3429), Point(3238, 3430), Point(3224, 3429),
             Point(3208, 3429), Point(3194, 3430), Point(3185, 3438),
             Point(3187, 3447), Point(3179, 3450), Point(3168, 3459),
             Point(3165, 3471), Point(3167, 3480), Point(3167, 3489)])];
    LOC_DB_BANK: pArr := [TPointArray([Point(3231, 3218), Point(3235, 3223), Point(3227, 3233),
             Point(3221, 3245), Point(3217, 3258), Point(3217, 3272),
             Point(3206, 3278), Point(3192, 3282), Point(3178, 3285),
             Point(3164, 3287), Point(3151, 3294), Point(3139, 3294),
             Point(3126, 3297), Point(3113, 3294), Point(3109, 3280),
             Point(3105, 3267), Point(3105, 3254), Point(3099, 3248),
             Point(3094, 3242)])];
    LOC_FE_BANK: pArr := [TPointArray([Point(3229, 3217), Point(3234, 3223), Point(3226, 3236),
             Point(3219, 3249), Point(3216, 3263), Point(3211, 3276),
             Point(3197, 3280), Point(3184, 3285), Point(3171, 3285),
             Point(3160, 3290), Point(3147, 3294), Point(3131, 3294),
             Point(3117, 3294), Point(3103, 3293), Point(3089, 3289),
             Point(3077, 3280), Point(3064, 3277), Point(3050, 3275),
             Point(3035, 3275), Point(3022, 3276), Point(3009, 3279),
             Point(3007, 3294), Point(3005, 3304), Point(3007, 3317),
             Point(3006, 3331), Point(3007, 3343), Point(3006, 3357),
             Point(3012, 3361), Point(3014, 3356)])];
    LOC_FW_BANK: pArr := [TPointArray([Point(3229, 3217), Point(3234, 3223), Point(3226, 3236),
             Point(3219, 3249), Point(3216, 3263), Point(3211, 3276),
             Point(3197, 3280), Point(3184, 3285), Point(3171, 3285),
             Point(3160, 3290), Point(3147, 3294), Point(3131, 3294),
             Point(3117, 3294), Point(3103, 3293), Point(3089, 3289),
             Point(3077, 3280), Point(3064, 3277), Point(3050, 3275),
             Point(3035, 3275), Point(3022, 3276), Point(3009, 3279),
             Point(3007, 3294), Point(3005, 3304), Point(3007, 3317),
             Point(3006, 3331), Point(3007, 3343), Point(3006, 3357),
             Point(2999, 3364), Point(2988, 3372), Point(2977, 3378),
             Point(2962, 3379), Point(2952, 3380), Point(2946, 3368)])];
    LOC_PS_DBOX: pArr := [TPointArray([Point(3228, 3218), Point(3237, 3219), Point(3230, 3230),
             Point(3223, 3242), Point(3217, 3253), Point(3216, 3267),
             Point(3209, 3277), Point(3196, 3280), Point(3185, 3283),
             Point(3171, 3286), Point(3157, 3291), Point(3143, 3293),
             Point(3130, 3296), Point(3117, 3294), Point(3105, 3293),
             Point(3092, 3288), Point(3077, 3283), Point(3071, 3273),
             Point(3057, 3265), Point(3044, 3258), Point(3041, 3244),
             Point(3047, 3233)])];
    LOC_EV_BANK: pArr := [TPointArray([Point(3230, 3217), Point(3242, 3224), Point(3257, 3226),
             Point(3260, 3238), Point(3254, 3250), Point(3251, 3263),
             Point(3242, 3275), Point(3239, 3290), Point(3241, 3306),
             Point(3251, 3317), Point(3264, 3324), Point(3278, 3332),
             Point(3287, 3345), Point(3296, 3356), Point(3296, 3369),
             Point(3294, 3382), Point(3292, 3396), Point(3290, 3409),
             Point(3285, 3422), Point(3279, 3429), Point(3264, 3428),
             Point(3252, 3429), Point(3238, 3430), Point(3224, 3429),
             Point(3208, 3429), Point(3194, 3430), Point(3189, 3429),
             Point(3176, 3428), Point(3164, 3423), Point(3150, 3417),
             Point(3137, 3417), Point(3123, 3417), Point(3112, 3422),
             Point(3099, 3420), Point(3092, 3430), Point(3092, 3441),
             Point(3096, 3453), Point(3099, 3465), Point(3097, 3478),
             Point(3088, 3485), Point(3089, 3492), Point(3093, 3491)])];
    LOC_SV_BANK:
                 begin
                   pArr := [];
                   obs := [];
                 end;
    LOC_AK_BANK: pArr := [TPointArray([Point(3229, 3217), Point(3238, 3221), Point(3247, 3224),
             Point(3260, 3235), Point(3256, 3248), Point(3251, 3260),
             Point(3245, 3273), Point(3240, 3286), Point(3238, 3300),
             Point(3247, 3312), Point(3258, 3323), Point(3271, 3331),
             Point(3284, 3332), Point(3285, 3316), Point(3286, 3301),
             Point(3289, 3285), Point(3292, 3270), Point(3293, 3254),
             Point(3295, 3237), Point(3282, 3228), Point(3282, 3213),
             Point(3284, 3198), Point(3283, 3183), Point(3278, 3170),
             Point(3270, 3165)])];
    LOC_KJ_STILES:
                  begin
                    pArr := [];
                    obs := [];
                  end;
    LOC_CB_BANK:
                 begin
                   pArr := [];
                   obs := [];
                 end;
  end;

  {Color or Reflection}
  if ReflectionBroken then
  begin
    Path := MSI_Convert2DTilePath(pArr);
    Method := @MSI_WalkColorPath;
  end else begin
    Path := pArr;
    Method := @MSI_WalkTilePath;
  end;

  {Attempt to walk to location without teleporting first}
  e := High(Path);
  for i := High(Path[e]) downto 0 do
  begin
    if ReflectionBroken then
      p := Point(Path[e][i].x, Path[e][i].y)
    else
      p := TileToMM(Path[e][i]);

    if rs_OnMinimap(p.x, p.y) then
      Result := Method(Path[e]);

    if Result then
      Break;
  end;

  {Teleport home}
  if (not Result) then
  begin
    MSI_LoadSpell(SPELL_TELE_HOME);
    MSI_ClickSpell(SPELL_TELE_HOME);

    for i := 0 to 25 do
    begin
      Wait(1000);
      if MSI_AtLocation(LOC_LB_SPAWN, false) then
        Break;
      if i = 20 then
      begin
        MSI_SubDebug('Failed to teleport home');
        MSI_CloseHeader('MSI_DeathWalk: '+BoolToStr(Result)+';');
        Exit;
      end;
    end;

    ClickNorth(True);

    {Walk back}
    for i := 0 to High(Path) do
    begin
      if (not Method(Path[i])) then
      begin
        MSI_SubDebug('Failed to walk index '+IntToStr(i)+' of path.');
        MSI_CloseHeader('MSI_DeathWalk: '+BoolToStr(Result)+';');
        Exit;
      end;
      {Handle obstacles}
      if Length(obs) > 0 then
        if (not MSI_SolveObstacle(obs[i])) then
        begin
          MSI_SubDebug('Failed to solve obstacle '+IntToStr(obs[i])+'.');
          MSI_CloseHeader('MSI_DeathWalk: '+BoolToStr(Result)+';');
          Exit;
        end;
      Result := MSI_AtLocation(Script.PathInfo.StartLoc, False);
      if Result then
        MSI_Players[CurrentPlayer].Location := Script.PathInfo.StartLoc;
    end;
  end;
end;

(*
MSI_WalkPath
~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_WalkPath(sLoc, eLoc: Integer): Boolean;

Walks the entire path from start (sLoc) to finish (eLoc), including obstacles.
Returns true if the player is at the ending location.

.. note::

  | Author: marpis & Coh3n
  | Last Updated: 27 February 2011 by Coh3n

Example:

.. code-block:: pascal

  // Walks to VE mine only if your player is set to use SCRIPT_VE_MINER
  MSI_WalkPath(LOC_VE_BANK, LOC_VE_MINE);
*)
function MSI_WalkPath(sLoc, eLoc: Integer): Boolean;
var
  ATPA: T2DPointArray;
  eTPA: TPointArray;
  Walked: function(TPA: TPointArray): Boolean;
  i, t, h, obsLen: Integer;
  Script: TScript;
  obsSolved: TBooleanArray;
label StartWalk;
begin
  if (not LoggedIn) then
    Exit;

  MSI_AddHeader('MSI_WalkPath');

  Script := MSI_Scripts[MSI_Players[CurrentPlayer].Scripts[CurrentScript].Name];
  SetLength(obsSolved, Length(Script.PathInfo.Obstacles));

  if (ReflectionBroken) then
  begin
    ATPA := MSI_LoadPath(sLoc, eLoc, True);
    eTPA := MSI_MirrorTPA(MSI_Locations[Script.PathInfo.EndLoc].Points);
    Walked := @MSI_WalkColorPath;
  end else begin
    ATPA := MSI_LoadPath(sLoc, eLoc, False);
    eTPA := MSI_MirrorTPA(MSI_Locations[Script.PathInfo.EndLoc].Tiles);
    Walked := @MSI_WalkTilePath;
  end;

  t := (GetSystemTime + (Length(ATPA) * 60 + 180) * 1000);
  obsLen := High(Script.PathInfo.Obstacles);

  if not RunEnergy(20) then
  begin
    RestUntil(40+Random(20));
    SetRun(true);
  end;

	repeat
		for i := 0 to High(ATPA) do
      with MSI_Players[CurrentPlayer] do
		  begin
      StartWalk:
        // Walk the leg of the path
				if (not (Walked(ATPA[i]))) then
          if (not (Walked(eTPA))) and (not (Walked(ATPA[i]))) then
				  begin
            MSI_Debug('Failed to walk leg '+IntToStr(i)+' of script: '+Capitalize(Script.Name));
            if MSI_DeathWalk then
              goto StartWalk;
            ReportInfo.FalseReason := 'Failed to walk to ' + MSI_Locations[eLoc].Name;
            MSI_CloseHeader('MSI_WalkPath: ' + BoolToStr(Result));
				    Location := LOC_LOST;
            Exit;
				  end;

        // Handle the obstacle
		    if (MSI_HandleObstacle(Script, sLoc, h)) then
        begin
          obsSolved[h] := True;
		      if (h <> obsLen) then
		        Inc(h);
        end;

        // Return true if the player is at the ending location
		    if (MSI_AtLocation(eLoc, True)) then
		    begin
		      Result := True;
		      Break;
		    end;
		  end;
	until(Result or (GetSystemTime > t));

	MSI_CloseHeader('MSI_WalkPath: ' + BoolToStr(Result));
end;

(*
MSI_PerformWalk
~~~~~~~~~~~~~~~

.. code-block:: pascal

  function MSI_PerformWalk(startLoc, endLoc: Integer): Boolean;

This is the function that is called in MSI_Walk (Mainloop.simba). It takes into
consideration both color and reflection paths depending on the user's preference
as well as the current state of reflection.

.. note::

  | Author: Coh3n
  | Last Updated: 18 May 2011 by Coh3n

Example:

.. code-block:: pascal

  // Walks to VE mine only if your player is set to use SCRIPT_VE_MINER
  MSI_PerformWalk(LOC_VE_BANK, LOC_VE_MINE);
*)
function MSI_PerformWalk(startLoc, endLoc: Integer): Boolean;
begin
  if (not LoggedIn) then
    Exit;

  MSI_FindRandoms(true);
  Result := MSI_WalkPath(startLoc, endLoc);
end;

